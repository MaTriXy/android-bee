buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.2'
    classpath 'com.f2prateek.checkstyle:checkstyle:1.0.1'
    classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.2'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'spoon'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 1
def versionBuild = 0

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    applicationId 'com.f2prateek.bee'

    minSdkVersion 15
    targetSdkVersion 25

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    testInstrumentationRunner "com.f2prateek.bee.BeeTestRunner"
  }

  dexOptions {
    // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isTravis
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
    }
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    abortOnError false
  }
}

configurations.all {
  resolutionStrategy {
    force 'com.android.support:support-annotations:25.3.1'
  }
}

dependencies {
  repositories {
    jcenter()
  }

  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'io.reactivex:rxandroid:1.0.1'
  compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
  compile 'com.jakewharton.timber:timber:4.0.1'
  compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.2'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.27'

  androidTestCompile 'junit:junit:4.12'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'
}
