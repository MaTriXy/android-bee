buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.1'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    classpath 'com.f2prateek.checkstyle:checkstyle:1.0.1'
    classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.3') {
      // Workaround for https://github.com/stanfy/spoon-gradle-plugin/issues/33
      exclude module: 'guava'
    }
    classpath 'com.google.guava:guava:17.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'checkstyle'
apply plugin: 'com.f2prateek.checkstyle'
apply plugin: 'spoon'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.1'

  defaultConfig {
    applicationId 'com.f2prateek.bee'

    minSdkVersion 15
    targetSdkVersion 23

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    testInstrumentationRunner "com.f2prateek.bee.BeeTestRunner"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  dexOptions {
    preDexLibraries = Boolean.valueOf(System.getProperty("pre-dex", "true"))
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
    }
  }
}

checkstyle {
  configFile rootProject.file('gradle/checkstyle.xml')
}

configurations.all {
  resolutionStrategy {
    force 'com.android.support:support-annotations:23.0.1'
  }
}

dependencies {
  repositories {
    jcenter()
  }

  compile 'com.android.support:appcompat-v7:23.0.1'
  compile 'io.reactivex:rxandroid:1.0.1'
  compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.jakewharton.timber:timber:4.0.1'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.27'

  androidTestCompile 'junit:junit:4.12'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'
  androidTestCompile 'com.squareup.spoon:spoon-client:1.2.0'
}
